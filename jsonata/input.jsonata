$.(
	$plaintexttitle := (
					$htmltags := /<\/?([a-z0-9]*)\b[^>]*>?/;
					$dash := /&(#8210;|#8211;|#8212;|#8213;|ndash;|mdash;|horbar;)/;
					$apos := /&(#8217;|apos;|rsquo;)/;
					$copy := /&(#169;|copy;)/;
					$tm := /&(#8482;|trade;)/;
					$registered := /&(#174;|reg;)/;
					$otherentities :=  /&(#?[\w\d]+;)/;

					$title := localizedMetadata[0].title ? $trim(localizedMetadata[0].title) : '';
					$title := $replace($title, $htmltags, '');
					$title := $replace($title, $dash, '-');
					$title := $replace($title, $copy, '(c)');
					$title := $replace($title, $registered, '(r)');
					$title := $replace($title, $tm, '(tm)');
					$title := $replace($title, $apos, $base64decode('Jw=='));
					$title := $replace($title, $otherentities, '');
                    
                    $title := $replace($title, '|', '-');
					$title;
    );
    
    $plaintextdescription := (
					$htmltags := /<\/?([a-z0-9]*)\b[^>]*>?/;
					$dash := /&(#8210;|#8211;|#8212;|#8213;|ndash;|mdash;|horbar;)/;
					$apos := /&(#8217;|apos;|rsquo;)/;
					$copy := /&(#169;|copy;)/;
					$tm := /&(#8482;|trade;)/;
					$registered := /&(#174;|reg;)/;
					$otherentities :=  /&(#?[\w\d]+;)/;

					$desc := localizedMetadata[0].description ? $trim(localizedMetadata[0].description) : '';
					$desc := $replace($desc, $htmltags, '');
					$desc := $replace($desc, $dash, '-');
					$desc := $replace($desc, $copy, '(c)');
					$desc := $replace($desc, $registered, '(r)');
					$desc := $replace($desc, $tm, '(tm)');
					$desc := $replace($desc, $apos, $base64decode('Jw=='));
					$desc := $replace($desc, $otherentities, '');
                    
                    $desc := $replace($desc, '|', '-');
					$desc;
			);
			
	$title300 := (
    				$input := $plaintexttitle;
					$maxlength := 300;
    				$wordboundary:= /\s(\w+)$/;
					$ellipsis := ' ...';
                    $trimlength := $maxlength - $length($ellipsis);

					$needtrim := ($length($input) > $maxlength);
					$trimmedinput := $needtrim ? $replace($trim($substring($input , 0, $trimlength)), $wordboundary, '') & $ellipsis;
					$needtrim ? $trimmedinput : $input;
			);

	$description2000 := (
    				$input := $plaintextdescription;
					$maxlength := 2000;
    				$wordboundary:= /\s(\w+)$/;
					$ellipsis := ' ...';
                    $trimlength := $maxlength - $length($ellipsis);

					$needtrim := ($length($input) > $maxlength);
                    $trimmedinput := $needtrim ? $replace($trim($substring($input , 0, $trimlength)), $wordboundary, '') & $ellipsis;
					
					$needtrim ? $trimmedinput : $input;
			);

	$sftype := (
      $result := $lookup([{
                      'BOOK': (
                          contentType.displayLabel = 'Book Summary' ? $uppercase(contentType.displayLabel);
                      ),
                      'AUDIOBOOK': (
					      contentType.displayLabel = 'Audiobook Summary' ? 'AUDIO SUMMARY';
                      ),
                      'LINKED_CONTENT': (
                          'LINKED CONTENT'
                      )
                  }
              ], contentType.percipioType);
          $result ? $result : contentType.percipioType
    );

	$sfcompletion := (
      $result := $lookup([{
                      'BOOK': (
                          $bookcomp := contentType.displayLabel = 'Book Summary' ? 'BOOK-SUMM-COMPL';
						  $bookcomp ? $bookcomp : 'BOOK-COMPL'
                      ),
                      'AUDIOBOOK': (
					      $audiobookcomp := contentType.displayLabel = 'Audiobook Summary' ? 'AUDIO-BK-SUMM-COMPL';
                          $audiobookcomp ? $audiobookcomp : 'AUDIO-BK-COMPL'
                      ),
                      'LINKED_CONTENT': (
                          'LINKED-CTNT-COMPL'
                      ),
					  'COURSE' : 'COURSE-COMPL',
					  'CHANNEL' : 'CHANNEL-COMPL',
					  'VIDEO' : 'VIDEO-COMPL'
                  }
              ], contentType.percipioType);
          $result ? $result : contentType.percipioType
    );

	$transformed := $.{
	'CPNT_ID': id,
	'CPNT_TYP_ID': $sftype,
	'NOTACTIVE': lifecycle.status = 'ACTIVE' ? 'N' : 'Y',
	'CPNT_TITLE':$title300,
	'DEL_MTH_ID': $sftype,
	'CPNT_DESC': $description2000,
	'CMPL_STAT_ID': $sfcompletion,
	'CREDIT_HRS': (
		$input := (duration = null ? '' : duration);
		$time := $substringAfter($input, 'T');
		$hours := $contains($time, 'H') ? $substringBefore($time, 'H') : '0';
		$remainder := $substringAfter($time, 'H');
		$minutes := $contains($remainder, 'M') ? $substringBefore($remainder, 'M') : '0';
		$remainder := $substringAfter($remainder, 'M');
		$seconds := $contains($remainder, 'S') ? $substringBefore($remainder, 'S') : '0';
		$result := ($number($hours) + ($number($minutes) / 60) + ($number($seconds) / 3600));
		$result = '0' ? '' : $round($result, 2)
	),
	'SHOW_IN_CATALOG': 'Y',
	'CATALOG_1': 'EXTERNAL',
	'APP_ID': associations.parent = null ? id : associations.parent.id,
	'BUILD_COMPANY': associations.parent = null ? 'Percipio' : associations.parent.title,
	'CONTENT_ONLINE': 'Y',
	'LAUNCH_TYPE': 3,
	'PRIMARY_PARAM': link,
	'ITEM_ONLINE': 'Y',
	'MODULE_NAME': $title300,
	'THUMBNAIL_URI': imageUrl,
	'LOCALE': $lookup([{
				'en-US': 'English',
				'en': 'English',
				'fr': 'French',
				'fr-FR': 'French',
				'de': 'German',
				'de-DE': 'German',
				'es': 'Spanish',
				'es-DO': 'Spanish'
			}
		], (localizedMetadata[0].localeCode ? localizedMetadata[0].localeCode : 'English')),
	'LAUNCH_IN_A_NEW_BWSR_WINDOW': 'Y',
	'REV_DTE': lifecycle.lastupDatedDate ? $fromMillis($toMillis(lifecycle.'lastUpdatedDate'), '[MNn,*-3]-[D01]-[Y0001] [H01]:[m01]:[s01][z]', '+0530') : '',
	'CPNT_SRC_ID': 'PERCIPIO',
	'CREATE_DTE': lifecycle.publishDate ? $fromMillis($toMillis(lifecycle.'publishDate'), '[MNn,*-3]-[D01]-[Y0001] [H01]:[m01]:[s01][z]', '+0530') : ''
}
)
